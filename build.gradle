plugins {
    id("java")
    id("maven-publish")
    id("signing")
    alias(libs.plugins.io.freefair.lombok)
    alias(libs.plugins.io.codearte.nexus.staging)
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = "com.github.differentway"
description = "Couchmove"
sourceCompatibility = "1.8"

test {
    useJUnitPlatform()
    exclude "**/*IT*", "**/*IntTest*"

    testLogging {
        events 'FAILED', 'SKIPPED'
    }
}
check.dependsOn test

task integrationTest(type: Test) {
    useJUnitPlatform()
    description = "Execute integration tests."
    group = "verification"
    include "**/*IT*", "**/*IntTest*"

    testLogging {
        events 'FAILED', 'SKIPPED'
    }
}
check.dependsOn integrationTest

dependencies {
    implementation(libs.couchbase.client)
    implementation(libs.slf4j.api)
    implementation(libs.intellij.annotations.java8)
    implementation(libs.commons.codec)
    implementation(libs.commons.io)
    implementation(libs.commons.lang)
    implementation(libs.guava.jre)
    implementation(libs.semver4j)

    testImplementation(libs.junit.jupiter.api)
    testImplementation(libs.junit.jupiter.engine)
    testImplementation(libs.junit.jupiter.params)
    testImplementation(libs.testcontainers.couchbase)
    testImplementation(libs.mockito.core)
    testImplementation(libs.mockito.junit.jupiter)
    testImplementation(libs.assertj.core)
    testImplementation(libs.logback.classic)
}

apply from: "gradle/publish.gradle"
